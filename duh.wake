tuple NodeModules =
  Dir   String
  Files List String
  Path  String

global def npmInstall _ =
  def cmdline = which "npm", "i", "github:sifive/duh#duh-wake", Nil
  def inputs = Nil
  def foutputs _ = files "node_modules/.bin" '.*' ++ files "node_modules/duh" '.*'
  def job =
    makeManualJob cmdline inputs foutputs
    | runJob
  def path = if job.status == 0 then "node_modules/.bin" else raise "\"npm i github:sifive/duh\" failed!"
  def dir = if job.status == 0 then "node_modules" else raise "\"npm i github:sifive/duh\" failed!"
  NodeModules dir job.outputs path

def whichNPMCommand nodeModules runDir cmd =
  def nodeModulesPath = nodeModules.getNodeModulesPath
  whichIn nodeModulesPath cmd | relative runDir

global def duhValidate json =
  def nodeModules = npmInstall 0
  def cmdline = nodeModules.whichNPMCommand "." "duh", "validate", json, Nil
  def inputs = json, nodeModules.getNodeModulesFiles
  def foutputs _ = Nil
  def validateJob =
    makeManualJob cmdline inputs foutputs
    #| setJobCache False
    | runJob
  validateJob.status == 0

global def duhGenerate json outputDir =
  def nodeModules = npmInstall 0
  def cmdline =
    def duh_scala = nodeModules.whichNPMCommand "." "duh-export-scala"
    duh_scala, json, "-o", outputDir, Nil
  def inputs = json, outputDir, nodeModules.getNodeModulesFiles
  def foutputs _ = json, files outputDir '.*-base\.scala'
  if duhValidate json
  then
    makeManualJob cmdline inputs foutputs
    | runJob
    | outputs
  else
    raise "Validate failed for {json}!"


global def duhGet json value =
  def nodeModules = npmInstall 0
  def cmdline =
    def duh = nodeModules.whichNPMCommand "." "duh"
    duh, "get", value, json, Nil
  def inputs = json, nodeModules.getNodeModulesFiles
  def foutputs _ = Nil
  def jobOut =
    makeManualJob cmdline inputs foutputs
    | runJob
    | stdout
  if matches "undefined" jobOut
  then
    raise "Value {value} is undefined in {json}"
  else
    jobOut

global def duhImportVerilogPorts json file =
  def inputs = file, json, nodeModules.getNodeModulesFiles
  def nodeModules = npmInstall 0
  def cmdline = nodeModules.whichNPMCommand "." "duh-import-verilog-ports", json, Nil
  def stdin = file
  def foutputs _ = json, Nil
  makeManualJob cmdline inputs foutputs
  | setJobStandardInput stdin
  | setJobCache False
  | runJob
  | output

global def duhInferChannels json =
  def nodeModules = npmInstall 0
  def inputs = json, nodeModules.getNodeModulesFiles
  def cmdline = nodeModules.whichNPMCommand "." "duh-infer-channels", json, Nil
  def foutputs _ = json, Nil
  makeManualJob cmdline inputs foutputs
  | setJobCache False
  | runJob
  | output
