tuple NodeModules =
  Dir   String
  Files List String
  Path  String

global def npmInstall _ =
  def cmdline = which "npm", "i", "github:sifive/duh", Nil
  def inputs = Nil
  def foutputs _ = files "node_modules/.bin" '.*' ++ files "node_modules/duh" '.*'
  def job =
    makeManualJob cmdline inputs foutputs
    | runJob
  def path = if job.status == 0 then "node_modules/.bin" else raise "\"npm i github:sifive/duh\" failed!"
  def dir = if job.status == 0 then "node_modules" else raise "\"npm i github:sifive/duh\" failed!"
  NodeModules dir job.outputs path

def getNPMEnvironment nodeModules runDir =
  def addNodeModulesPath env =
    def regex = 'PATH=(.*)'
    def oldPathOpt =
      match (filter (matches regex) env)
        Nil  = None
        h, t = Some (extract regex h).head
    def relNodePath = relative runDir nodeModules.getNodeModulesPath
    def newPath = match oldPathOpt
      None   = "PATH={relNodePath}"
      Some p = "PATH={relNodePath}:{p}"
    def oldEnv = filter (\e !(matches regex e)) env
    newPath, oldEnv
  addNodeModulesPath environment

def whichNPMCommand nodeModules runDir cmd =
  def nodeModulesPath = relative runDir nodeModules.getNodeModulesPath
  whichIn nodeModulesPath cmd | relative runDir

global def npmValidate json =
  def nodeModules = npmInstall 0
  def cmdline = nodeModules.whichNPMCommand "." "duh", "validate", json, Nil
  def inputs = json, nodeModules.getNodeModulesFiles
  def env = nodeModules.getNPMEnvironment "."
  def foutputs _ = json, Nil
  def validateJob =
    makeManualJob cmdline inputs foutputs
    | setJobEnvironment env
    | runJob
  validateJob.status == 0

global def duhGenerate json rawOutputDir =
  def nodeModules = npmInstall 0
  def outputDir = mkdir 0775 rawOutputDir
  def cmdline =
    def duh_scala = nodeModules.whichNPMCommand outputDir "duh-export-scala"
    def relJson = relative outputDir json
    duh_scala, relJson, Nil
  def inputs = json, outputDir, nodeModules.getNodeModulesFiles
  def foutputs _ = json, Nil
  if npmValidate json
  then
    def env = nodeModules.getNPMEnvironment outputDir
    makeManualJob cmdline inputs foutputs
    | setJobDirectory outputDir
    | setJobEnvironment env
    | runJob
    | outputs
  else
    raise "Validate failed for {json}!"


global def duhGet json value =
  def nodeModules = npmInstall 0
  def cmdline =
    def duh = nodeModules.whichNPMCommand "." "duh"
    duh, "get", value, json, Nil
  def inputs = json, nodeModules.getNodeModulesFiles
  def foutputs _ = Nil
  def jobOut =
    def env = nodeModules.getNPMEnvironment "."
    makeManualJob cmdline inputs foutputs
    | setJobEnvironment env
    | runJob
    | stdout
  if matches "undefined" jobOut
  then
    raise "Value {value} is undefined in {json}"
  else
    jobOut

global def duhImportVerilogPorts json file =
  def inputs = file, json, nodeModules.getNodeModulesFiles
  def nodeModules = npmInstall 0
  def cmdline = nodeModules.whichNPMCommand "." "duh-import-verilog-ports", json, Nil
  def stdin = file
  def env = nodeModules.getNPMEnvironment "."
  def foutputs _ = json, Nil
  makeManualJob cmdline inputs foutputs
  | setJobEnvironment env
  | setJobStandardInput stdin
  | runJob
  | output

global def duhInferChannels json =
  def nodeModules = npmInstall 0
  def inputs = json, nodeModules.getNodeModulesFiles
  def cmdline = nodeModules.whichNPMCommand "." "duh-infer-channels", json, Nil
  def env = nodeModules.getNPMEnvironment "."
  def foutputs _ = json, Nil
  makeManualJob cmdline inputs foutputs
  | setJobEnvironment env
  | runJob
  | output
